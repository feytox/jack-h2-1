/** Класс, отвечающий за типы блоков. */
class Block {
    static Block air;
    static Block stone;
    static Block money;
    static Block key;
    static Block finish;

    field int id;
    field boolean moveThrough;

    constructor Block new(int blockId, boolean canMoveThrough){
        let id = blockId;
        let moveThrough = canMoveThrough;
        return this;
    }

    /** Инициализирует типы блоков */
    function void initBlocks(){
        let air = Block.new(0, true);
        let stone = Block.new(1, false);
        let money = Block.new(2, true);
        let key = Block.new(3, true);
        let finish = Block.new(4, true);
        return;
    }

    /** Парсит блок из символа */
    function Block parse(char blockChar){
        if ((blockChar = 32) | (blockChar = 80)){ // ' ' or 'P' = air
            return air;
        }

        if (blockChar = 35){ // '#' = stone
            return stone;
        }

        if (blockChar = 77){ // 'M' = money
            return money;
        }

        if (blockChar = 75){ // 'K' = key
            return key;
        }

        if (blockChar = 70){ // 'F' = finish
            return finish;
        }

        do Exception.throw("Block: invalid block");
        return 0;
    }

    /** Отрисовывает блок с учётом координаты левого края и размеров */
    method void render(int x, int y, int width, int height){
        if (id = 0){  // air
            return;
        }
        if (id = 1){  // stone
            do _renderFull(x, y, width, height);
        }
        if (id = 2){  // money
            do _renderCoin(x, y, width, height);
        }
        if (id = 3){  // key
            do _renderKey(x, y, width, height);
        }
        if (id = 4){  // finish
            do _renderFinish(x, y, width, height);
        }
        return;
    }

    /** Рендерит полностью закрашенный квадрат */
    method void _renderFull(int x, int y, int width, int height){
        // TODO: оптимизировать
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    method void _renderCoin(int x, int y, int width, int height) {
        var int location, memAddress;
        let location = ((y * 32) + (x / 16));
        let memAddress = 16384 + location;
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 960);
        do Memory.poke(memAddress + 64, 4080);
        do Memory.poke(memAddress + 96, 7224);
        do Memory.poke(memAddress + 128, 15324);
        do Memory.poke(memAddress + 160, 12876);
        do Memory.poke(memAddress + 192, 25158);
        do Memory.poke(memAddress + 224, 25542);
        do Memory.poke(memAddress + 256, 24646);
        do Memory.poke(memAddress + 288, 24646);
        do Memory.poke(memAddress + 320, 12364);
        do Memory.poke(memAddress + 352, 14428);
        do Memory.poke(memAddress + 384, 7224);
        do Memory.poke(memAddress + 416, 4080);
        do Memory.poke(memAddress + 448, 960);
        do Memory.poke(memAddress + 480, 0);
        return;
    }

    /** Рендерит ключ */
    method void _renderKey(int x, int y, int width, int height) {
        var int location, memAddress;
        let location = ((y * 32) + (x / 16));
        let memAddress = 16384 + location;
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 0);
        do Memory.poke(memAddress + 64, 0);
        do Memory.poke(memAddress + 96, 0);
        do Memory.poke(memAddress + 128, 0);
        do Memory.poke(memAddress + 160, 28);
        do Memory.poke(memAddress + 192, 38);
        do Memory.poke(memAddress + 224, 13814);
        do Memory.poke(memAddress + 256, 32766);
        do Memory.poke(memAddress + 288, 62);
        do Memory.poke(memAddress + 320, 28);
        do Memory.poke(memAddress + 352, 0);
        do Memory.poke(memAddress + 384, 0);
        do Memory.poke(memAddress + 416, 0);
        do Memory.poke(memAddress + 448, 0);
        do Memory.poke(memAddress + 480, 0);
        return;
    }

    /** Рендерит ромб (финиш) */
    method void _renderFinish(int x, int y, int width, int height) {
        var int centerX, centerY;
        var Array diamondX, diamondY;
        
        let centerX = x + (width / 2);
        let centerY = y + (height / 2);
        
        // Создаем массивы для координат ромба
        let diamondX = Array.new(4);
        let diamondY = Array.new(4);
        
        // Задаем координаты ромба
        let diamondX[0] = centerX;      // Верхняя точка
        let diamondX[1] = centerX + 4;  // Правая точка
        let diamondX[2] = centerX;      // Нижняя точка
        let diamondX[3] = centerX - 4;  // Левая точка
        
        let diamondY[0] = centerY - 4;  // Верхняя точка
        let diamondY[1] = centerY;      // Правая точка
        let diamondY[2] = centerY + 4;  // Нижняя точка
        let diamondY[3] = centerY;      // Левая точка
        
        // Рисуем линии ромба
        do Screen.drawLine(diamondX[0], diamondY[0], diamondX[1], diamondY[1]);
        do Screen.drawLine(diamondX[1], diamondY[1], diamondX[2], diamondY[2]);
        do Screen.drawLine(diamondX[2], diamondY[2], diamondX[3], diamondY[3]);
        do Screen.drawLine(diamondX[3], diamondY[3], diamondX[0], diamondY[0]);
        
        // Освобождаем память
        do diamondX.dispose();
        do diamondY.dispose();
        return;
    }

    /** Возвращает ID блока */
    method int getId(){
        return id;
    }

    /** Проверяет, является ли блок монеткой */
    method boolean isCoin(){
        return getId() = 2;  // 2 - это ID монетки
    }

    /** Проверяет, является ли блок ключом */
    method boolean isKey(){
        return getId() = 3;  // 3 - это ID ключа
    }

    /** Проверяет, является ли блок финишем */
    method boolean isFinish() {
        return getId() = 4;  // 4 - это ID финиша
    }

    /** Проверяет, можно ли пройти через блок */
    method boolean canMoveThrough() {
        return moveThrough;
    }

    /** Возвращает блок воздуха */
    function Block getAir(){
        return air;
    }
}
