/** Класс, отвечающий за типы блоков. */
class Block {
    static Block air;
    static Block stone;
    static Block money;
    static Block key;

    field int id;
    field boolean moveThrough;

    constructor Block new(int blockId, boolean canMoveThrough){
        let id = blockId;
        let moveThrough = canMoveThrough;
        return this;
    }

    /** Инициализирует типы блоков */
    function void initBlocks(){
        let air = Block.new(0, true);
        let stone = Block.new(1, false);
        let money = Block.new(2, true);
        let key = Block.new(3, true);
        return;
    }

    /** Парсит блок из символа */
    function Block parse(char blockChar){
        if ((blockChar = 32) | (blockChar = 80)){ // ' ' or 'P' = air
            return air;
        }

        if (blockChar = 35){ // '#' = stone
            return stone;
        }

        if (blockChar = 77){ // 'M' = money
            return money;
        }

        if (blockChar = 75){ // 'K' = key
            return key;
        }

        do Exception.throw("Block: invalid block");
        return 0;
    }

    /** Отрисовывает блок с учётом координаты левого края и размеров */
    method void render(int x, int y, int width, int height){
        if (id = 0){  // air
            return;
        }
        if (id = 1){  // stone
            do _renderFull(x, y, width, height);
        }
        if (id = 2){  // money
            do _renderCoin(x, y, width, height);
        }
        if (id = 3){  // key
            do _renderKey(x, y, width, height);
        }
        return;
    }

    /** Рендерит полностью закрашенный квадрат */
    method void _renderFull(int x, int y, int width, int height){
        // TODO: оптимизировать
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    method void _renderCoin(int x, int y, int width, int height) {
        var int centerX, centerY, radius;
        let centerX = x + (width / 2);
        let centerY = y + (height / 2);
        let radius = Math.min(width, height) / 4;  // Радиус монетки
        
        do Screen.drawCircle(centerX, centerY, radius);
        return;
    }

    /** Рендерит ключ */
    method void _renderKey(int x, int y, int width, int height){
        var int centerX, centerY;
        var Array triangleX, triangleY;
        
        let centerX = x + (width / 2);
        let centerY = y + (height / 2);
        
        // Создаем массивы для координат треугольника
        let triangleX = Array.new(3);
        let triangleY = Array.new(3);
        
        // Задаем координаты треугольника
        let triangleX[0] = centerX;           // Вершина
        let triangleX[1] = centerX - 4;       // Левый нижний угол
        let triangleX[2] = centerX + 4;       // Правый нижний угол
        
        let triangleY[0] = centerY - 4;       // Вершина
        let triangleY[1] = centerY + 4;       // Левый нижний угол
        let triangleY[2] = centerY + 4;       // Правый нижний угол
        
        // Рисуем линии треугольника
        do Screen.drawLine(triangleX[0], triangleY[0], triangleX[1], triangleY[1]);
        do Screen.drawLine(triangleX[1], triangleY[1], triangleX[2], triangleY[2]);
        do Screen.drawLine(triangleX[2], triangleY[2], triangleX[0], triangleY[0]);
        
        // Освобождаем память
        do triangleX.dispose();
        do triangleY.dispose();
        return;
    }

    /** Возвращает ID блока */
    method int getId(){
        return id;
    }

    /** Проверяет, является ли блок монеткой */
    method boolean isCoin(){
        return getId() = 2;  // 2 - это ID монетки
    }

    /** Проверяет, является ли блок ключом */
    method boolean isKey(){
        return getId() = 3;  // 3 - это ID ключа
    }

    /** Проверяет, можно ли пройти через блок */
    method boolean canMoveThrough() {
        return moveThrough;
    }

    /** Возвращает блок воздуха */
    function Block getAir(){
        return air;
    }
}
