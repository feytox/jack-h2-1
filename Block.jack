/** Класс, отвечающий за типы блоков. */
class Block {
    static Block air;
    static Block stone;
    static Block money;

    field int id;
    field boolean moveThrough;

    constructor Block new(int blockId, boolean canMoveThrough){
        let id = blockId;
        let moveThrough = canMoveThrough;
        return this;
    }

    /** Инициализирует типы блоков */
    function void initBlocks(){
        let air = Block.new(0, true);
        let stone = Block.new(1, false);
        let money = Block.new(2, true);
        return;
    }

    /** Парсит блок из символа */
    function Block parse(char blockChar){
        if ((blockChar = 32) | (blockChar = 80)){ // ' ' or 'P' = air
            return air;
        }

        if (blockChar = 35){ // '#' = stone
            return stone;
        }

        if (blockChar = 77){ // 'M' = money
            return money;
        }

        do Exception.throw("Block: invalid block");
        return 0;
    }

    /** Отрисовывает блок с учётом координаты левого края и размеров */
    method void render(int x, int y, int width, int height){
        if (id = 0){
            return;
        }
        if (id = 1){
            do _renderFull(x, y, width, height);
        }
        if (id = 2){
            do _renderCoin(x, y, width, height);
        }
        return;
    }

    /** Рендерит полностью закрашенный квадрат */
    method void _renderFull(int x, int y, int width, int height){
        // TODO: оптимизировать
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    method void _renderCoin(int x, int y, int width, int height){
        // TODO: оптимизировать
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }
}
