/** Класс, отвечающий за типы блоков. */
class Block {
    static Block air;
    static Block stone;

    field int id;
    field boolean moveThrough;

    constructor Block new(int blockId, boolean canMoveThrough){
        let id = blockId;
        let moveThrough = canMoveThrough;
        return this;
    }

    /** Инициализирует типы блоков */
    function void initBlocks(){
        let air = Block.new(0, true);
        let stone = Block.new(1, false);
        return;
    }

    /** Парсит блок из символа */
    function Block parse(char blockChar){
        if ((blockChar = 32) | (blockChar = 80)){ // ' ' or 'P' = air
            return air;
        }

        if (blockChar = 35){ // '#' = stone
            return stone;
        }

        do Exception.throw("Block: invalid block");
        return 0;
    }

    /** Отрисовывает блок с учётом координаты левого края и размеров */
    method void render(int x, int y, int width, int height){
        if (id = 0){
            return;
        }
        if (id = 1){
            do _renderFull(x, y, width, height);
        }
        return;
    }

    /** Рендерит полностью закрашенный квадрат */
    method void _renderFull(int x, int y, int width, int height){
        var int i, j;
        var int endX, endY;
        
        let endX = x + width;
        let endY = y + height;
        
        // рисует построчно для оптимизации
        while (y < endY) {
            let i = x;
            while (i < endX) {
                do Screen.setColor(true);
                do Screen.drawPixel(i, y);
                let i = i + 1;
            }
            let y = y + 1;
        }
        return;
    }
}
